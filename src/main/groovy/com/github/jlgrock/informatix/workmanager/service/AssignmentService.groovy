package com.github.jlgrock.informatix.workmanager.service
import com.github.jlgrock.informatix.workmanager.domain.*
import com.github.jlgrock.informatix.workmanager.repository.AssignmentRepository
import com.github.jlgrock.informatix.workmanager.repository.BatchRepository
import com.github.jlgrock.informatix.workmanager.repository.UserRepository
import com.github.jlgrock.informatix.workmanager.web.rest.dto.AssignmentDTO
import com.github.jlgrock.informatix.workmanager.web.rest.dto.AttachmentDTO
import com.github.jlgrock.informatix.workmanager.web.rest.dto.HoursDTO
import com.github.jlgrock.informatix.workmanager.web.rest.errors.AssignmentException
import org.springframework.beans.factory.annotation.Autowired
import org.springframework.dao.EmptyResultDataAccessException
import org.springframework.http.MediaType
import org.springframework.stereotype.Service

import javax.transaction.Transactional
import java.time.LocalDateTime
/**
 *
 */
@Service
@Transactional
class AssignmentService {

    @Autowired
    AssignmentRepository assignmentRepository

    @Autowired
    UserRepository userRepository

    @Autowired
    BatchRepository batchRepository

    @Autowired
    BatchService batchService

    Iterable<Assignment> getAll() {
        assignmentRepository.findAll()
    }

    AssignmentDTO get(Long id) {
        new AssignmentDTO(assignmentRepository.findOne(id))
    }

    AssignmentDTO delete(Long id) {
        Assignment assignment = assignmentRepository.findOne(id)
        try {
            assignmentRepository.delete(id)
        } catch (EmptyResultDataAccessException e) {
            //NOOP
        }
        batchService.updateBatchStatus(assignment.batch.id)
        new AssignmentDTO(assignment)
    }

    AssignmentDTO addNewAssignment(Long userId, Long batchId, String name,
                                String contentType, byte[] data) {
        Assignment assignment = addAssignment(userId, batchId, name, contentType, data, AssignmentStatus.UNSTARTED)
        batchService.updateBatchStatus(assignment.batch.id)
        new AssignmentDTO(assignment)
    }
    AssignmentDTO addAssignment(Long userId, Long batchId, String name,
                                String contentType, byte[] data, AssignmentStatus status) {

        User user = userRepository.findOne(userId)
        Batch batch = batchRepository.findOne(batchId)

        Attachment original = new Attachment(
                fileName: name,
                uploadDate: LocalDateTime.now(),
                numberOfBytes: data.size(),
                mediaType: MediaType.parseMediaType(contentType),
                data: data);

        // TODO process file to determine number of records
        Assignment assignment = new Assignment([
                batch: batch,
                userAccount: user,
                numberOfRecords: 34, //TODO fix after processing
                original: original,
                isAutoGenerated: false,
                status: status
        ])
        AssignmentDTO retVal = new AssignmentDTO(assignmentRepository.save(assignment))

        //TODO send email

        retVal

    }

    AssignmentDTO completeAssignment(Long assignmentId, String name, String contentType, byte[] data) {
        Assignment assignment = assignmentRepository.findOne(assignmentId)

        Attachment completed = new Attachment([
                fileName: name,
                uploadDate: LocalDateTime.now(),
                numberOfBytes: data.size(),
                mediaType: MediaType.parseMediaType(contentType),
                data: data
        ])

        assignment.completed.add(completed)

        batchService.updateBatchStatus(assignment.batch.id)
        assignment.status = determineAssignmentStatus(assignment)
        assignment = assignmentRepository.save(assignment)
        new AssignmentDTO(assignment)
    }

    AssignmentStatus determineAssignmentStatus(Assignment assignment) {
        if (assignment.billableHours != null) {
            return AssignmentStatus.COMPLETED
        }

        if (assignment.completed != null && assignment.completed.size() > 0) {
            // TODO adjust status based on criteria.  Currently just picking NEEDS_HOURS all the time, FAILED can't be achieved
            return AssignmentStatus.NEEDS_HOURS
        }

        return AssignmentStatus.UNSTARTED
    }

    Assignment getAssignmentForHours(Long id) {
        Assignment assignment = assignmentRepository.findOne(id)

        if (assignment.completed == null) {
            throw new AssignmentException("Cannot set hours until assignment has been completed")
        }
        assignment
    }
    Assignment setHours(Long assignmentId, HoursDTO hoursDTO) {
        Assignment assignment = getAssignmentForHours(assignmentId)
        assignment.billableHours = hoursDTO.hours

        assignment.status = determineAssignmentStatus(assignment)
        assignment = assignmentRepository.save(assignment)
        batchService.updateBatchStatus(assignment.batch.id)
        assignment
    }

    Assignment clearHours(Long assignmentId) {
        Assignment assignment = getAssignmentForHours(assignmentId)
        assignment.billableHours = null

        batchService.updateBatchStatus(assignment.batch.id)
        assignment.status = determineAssignmentStatus(assignment)
        assignment = assignmentRepository.save(assignment)
        assignment
    }

    Collection<AttachmentDTO> getCompletedHistory(Long assignmentId) {
        Assignment assignment = assignmentRepository.findOne(assignmentId)
        assignment.completed.collect {
            Attachment attachment ->
            new AttachmentDTO(attachment)
        }
    }
}
